Recull Comandes Exercici 2 - Grup 4
-----------------------------------

1) Del padró de 1866 de Castellví de Rosanes (CR), retorna el número d'habitants i la llista de noms.
   Elimina duplicats i nan.

	MATCH (h:Habitatge)-[:VIU]-(p:Individu)
	WHERE h.Any_Padro = 1866 AND h.Municipi = "CR" AND p.name <> "nan"
	RETURN count(p.name) AS `Número d'habitants`, collect(distinct p.name) AS Habitants

	- Hem eliminat els noms duplicats a la llista de noms, tal i com demana l'enunciat, encara 
	  que els noms siguin de persones diferents.

	Per no haver de duplicar noms però tampoc haver d'eliminar el nom de dues persones diferents 
	que s'anomenin igual, hem fet el següent:

	MATCH (h:Habitatge)-[:VIU]-(p:Individu)
	WHERE h.Any_Padro = 1866 AND h.Municipi = "CR" AND p.name <> "nan"
	RETURN count(p.name) AS `Número d'habitants`, collect(distinct (p.name+ " " + p.surname)) AS Habitants

   Per a que el resultat doni igual que la solució del joc de proves:

	MATCH (h:Habitatge)-[:VIU]-(p:Individu)
	WHERE h.Any_Padro = 1866 AND h.Municipi = "CR" AND p.surname <> "nan"
	RETURN count(p.surname) AS `Número d'habitants`, collect(distinct p.surname) AS Habitants


2) Dels padrons de Sant Feliu de Llobregat (SFLL) d'abans de l'any 1840 (no inclòs), retorna la 
   població, l'any del padró i la llista d'identificadors dels habitatges de cada padró. Ordena els
   resultats per l'any de padró.

	MATCH (h:Habitatge)
	WHERE h.Any_Padro < 1840 AND h.Municipi = "SFLL" 
	RETURN h.Municipi AS `Població`, h.Any_Padro As `Any del padró`, collect(h.Id_Llar) AS `Identificador de l'habitatge`
	ORDER BY `Identificador de l'habitatge`

   Per a que el resultat doni igual que la solució del joc de proves:

	MATCH (h:Habitatge)
	WHERE h.Any_Padro < 1840 AND h.Municipi = "SFLL" 
	RETURN h.Any_Padro As `Any del padró`, collect(h.Id_Llar) AS `Identificador de l'habitatge`
	ORDER BY `Identificador de l'habitatge`


3) Retorna el nom de les persones que vivien al mateix habitatge que "rafel marti" (no té segon 
   cognom) segons el padró de 1838 de Sant Feliu de Llobregat (SFLL).
   Retorna la informació en mode graf i mode llista.

   Resultat com a taula:

	MATCH (p1:Individu)-[:VIU]-(h:Habitatge)-[:VIU]-(p2:Individu)
	WHERE p1.name = "rafel" AND p1.surname = "marti" AND h.Any_Padro = 1838 AND h.Municipi = "SFLL"
	RETURN collect(p2.name)

   Resultat com a graf:

	MATCH path = (p1:Individu)-[:VIU]-(h:Habitatge)-[:VIU]-(p2:Individu)
	WHERE p1.name = "rafel" AND p1.surname = "marti" AND h.Any_Padro = 1838 AND h.Municipi = "SFLL"
	RETURN path


4) Retorna totes les aparicions de "Miguel ballester". Fes servir la relació SAME_AS per poder retornar 
   totes les instancies, independentment de si hi ha variacions lèxiques (ex. diferents formes d'escriure 
   el seu nom/cognoms). Mostra la informació en forma de subgraf.

   Resultat (la imatge del subgraf es troba a l'informe):
   
   	MATCH (p1:Individu{})-[:SAME_AS]->(p2:Individu)
	WHERE toLower(p1.name) STARTS WITH "mi" AND toLower(p1.surname) STARTS WITH "ballest"
	RETURN p1,p2


5) Mostra totes les persones relacionades amb "antonio farran". Mostra la informació en forma de taula: 
   el nom, cognom1, cognom2, i tipus de relació.
   
	MATCH (p1:Individu)-[r]->(p2:Individu)
	WHERE p1.name= "antonio" AND p1.surname = "farran"
	RETURN p2.name AS nom, p2.surname AS cognom1, p2.second_surname AS cognom2,
	type(r) AS tipus_relacio	


6) Llisteu totes les relacions familiars que hi ha.
   
   	MATCH (:Individu)-[r:FAMILIA]-(:Individu)
	WHERE NOT r.relacio_harmonitzada = "null"
	RETURN collect(DISTINCT r.relacio_harmonitzada) AS relacions_familiars


7) Identifiqueu els nodes que representen el mateix habitatge (carrer i numero) al llarg dels anys de Sant Feliu del Llobregat (SFLL). Mostreu el resultat dels habitatges que tingueu totes dues informacions (carrer i numero), el nombre total d’habitatges, el llistat d’anys dels padrons i el llistat de les Ids de les llars. Ordeneu de més a menys segons el total d’habitatges i mostreu-ne els 10 primers.

	MATCH (h:Habitatge {Municipi:'SFLL'}), (h1: Habitatge {Municipi: 'SFLL'}) 
	WHERE h.Carrer = h1.Carrer AND h.Numero = h1.Numero 
	RETURN h.Carrer as Carrer, h.Numero as Número, size(collect(DISTINCT(h.Any_Padro))) as Número_total_habitatges, collect(DISTINCT(h.Any_Padro)) as llistat_anys, collect(DISTINCT(h.Id_Llar)) as llistat_Ids 
	ORDER BY Número_total_habitatges DESC,h.Carrer LIMIT 10


8) Mostreu les famílies de Castellví de Rosanes amb més de 3 fills. Mostreu el nom i cognoms del cap de família i el nombre de fills. Ordeneu-les pel nombre de fills fins a un límit de 20, de més a menys.	

	MATCH (h:Habitatge {Municipi: 'CR'})<-[:VIU]-(p:Individu)-[fcap:FAMILIA]-(p)-[ffill:FAMILIA]->(p2:Individu)
	WHERE (ffill.relacio_harmonitzada ="fill" OR ffill.relacio_harmonitzada="filla") AND fcap.relacio_harmonitzada="jefe"
	WITH p, size(collect(distinct p2)) AS numfills
	WHERE numfills > 3
	RETURN p.name AS nom, p.surname AS primer_cognom, p.second_surname AS segon_cognom, numfills AS total ORDER BY total DESC LIMIT 20


9) Mitja de fills a Sant Feliu del Llobregat l’any 1881 per família. Mostreu el total de
fills, el nombre d’habitatges i la mitja.

	MATCH (h: Habitatge{Municipi:"SFLL", Any_Padro:1881})-[:VIU]-(p1:Individu)
	CALL {
	    WITH p1
	    OPTIONAL MATCH (p1)-[f:FAMILIA {relacio_harmonitzada:"fill"}]->(p2:Individu)
	    RETURN p2
	UNION
	    WITH p1
	    OPTIONAL MATCH (p1)-[f:FAMILIA {relacio_harmonitzada:"filla"}]->(p2:Individu)
	    RETURN p2
	}
	RETURN count(distinct(h)) AS total_llars, count(p2) AS total_fills, toFloat(count(distinct(p1)))/count(distinct(h)) AS mitja


10) Per cada any que hi ha a la base de dades, quin és el carrer amb menys habitants de
Sant Feliu de Llobregat?

	MATCH (p:Individu)-[:VIU]-(h:Habitatge{Municipi: "SFLL"})
	WITH count(DISTINCT(p)) AS pers, h.Any_Padro AS any_padro, h.Carrer AS carrer
	WITH min([pers, carrer]) AS lst, any_padro
	RETURN any_padro, lst[1] AS carrer, lst[0] AS num_habitants
